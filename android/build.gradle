buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["MiddlewareReactNative_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["MiddlewareReactNative_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.middlewarereactnative"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
  maven {
    url = uri("https://s01.oss.sonatype.org/content/releases")
  }
}

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
  implementation ('io.github.middleware-labs:android-sdk:2.0.1') {
    exclude group: 'org.apache.commons', module: 'commons-compress'
    exclude group: 'commons-codec', module: 'commons-codec'
    exclude group: 'commons-io', module: 'commons-io'
  }
  implementation ('org.apache.commons:commons-compress:1.26.0') {
    exclude group: 'commons-io', module: 'commons-io'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }
  implementation 'commons-io:commons-io:2.14.0'
  implementation 'org.apache.commons:commons-lang3:3.13.0'
  implementation 'commons-codec:commons-codec:1.16.0'
  implementation 'io.opentelemetry.android:instrumentation:+'
  implementation 'io.opentelemetry:opentelemetry-sdk:+'
  runtimeOnly 'io.opentelemetry:opentelemetry-semconv:1.30.1-alpha'
  implementation 'io.opentelemetry:opentelemetry-exporter-otlp:+'
}

